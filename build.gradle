buildscript {
    repositories {
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }

    dependencies {
        classpath "nl.eveoh:gradle-aspectj:2.0"
    }
}

group 'org.apilytic'
version '1.0-SNAPSHOT'

project.ext {
    aspectjVersion = '1.8.10'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'aspectj'
apply plugin: 'application'
apply plugin: 'jacoco'

mainClassName = 'org.apilytic.currency.HelloWorld'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

ext.deps = [
        springVersion    : '5.0.6.RELEASE',
        springDataVersion: '1.8.0.RELEASE',
        jackson2Version  : '2.9.0.pr3',
        junitVersion     : '5.2.0',
]

compileAspect {
    xlint = 'warning'
}

dependencies {
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + deps.junitVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + deps.junitVersion
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.mockito:mockito-junit-jupiter:2.+'
    testCompile 'org.springframework:spring-test:' + deps.springVersion
    compile 'org.springframework:spring-core:' + deps.springVersion
    compile 'org.springframework:spring-web:' + deps.springVersion
    compile 'org.springframework:spring-aspects:' + deps.springVersion
    compile 'org.springframework.data:spring-data-redis:' + deps.springDataVersion
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile 'redis.clients:jedis:2.9.0'
    compile 'com.fasterxml.jackson.core:jackson-core:' + deps.jackson2Version
    compile 'com.fasterxml.jackson.core:jackson-databind:' + deps.jackson2Version
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport